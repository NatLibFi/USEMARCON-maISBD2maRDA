MARC 21 (RDA) --> MARC 21 (ISBD)
Kansalliskirjasto / 3.6.2016 (TV)

#define macro LYHENTEET | Replace('painos' By 'p.'); \
                          Replace('karttoja' By 'kartt.'); \
                          Replace('nuotteja' By 'nuott.'); \
                          Replace('kuvitettu' By 'kuv.'); \
                          Replace('sivua' By 's.'); \
                          Replace('painos' By 'p.'); \
                          Replace('uudistettu' By 'uud.')

#define macro TUNNUKSET | RegReplace(' Oy', ''); \
                          RegReplace(' A[Bb]', ''); \
                          RegReplace(' Ltd', '')

#define macro KUSTANNUSOY   | Replace('Oy' By ''); \
                              Replace('OY' By ''); \
                              Replace('Oü' By ''); \
                              Replace('OÜ' By ''); \
                              Replace('Ab' By ''); \
                              Replace('AB' By ''); \
                              Replace('Ltd' By ''); \
                              RegReplace('Kustannus(.)*\\s', ''); \
                              RegReplace('[Kk]ustannuspaikka tuntematon', 'S.l.'); \
                              RegReplace('[Kk]ustantaja tuntematon', 's.n.')

000/19/   | 000/19/ | 'i'
000/20-24/  | 000/20-24/ | S

008/1-6/   | 008/1-6/       | S
008/7/     | 008/7/         | Table ('aikaindy.tbl')
008/8-11/  | 008/8-11/      | S

#if (@008/7/ = 't')
008/12-15/  | 008/12-15/    | '    '
#endif

#if (Not @008/7/ = 't')
008/12-15/  | 008/12-15/    | S
#endif

008/16-18/ | 008/16-18/     | Table ('maakoodity.tbl')
008/19-35/ | 008/19-35/     | S
008/36-38/ | 008/36-38/     | Table ('kielikoodit.tbl')
008/39-40/ | 008/39-40/     | '  '

015I1 | 015I1 | S
015I2 | 015I2 | S
015$q | 015$q | Replace('.,' By ',');
                Delete('(');
                Delete(')');
                Delete('.', At Ending);
                Table ('Sidosasut.tbl')
015$? | 015$? | S

020I1 | 020I1 | S
020I2 | 020I2 | S
020$a | 020$a | S; RegReplace('[\\s](.*)', '')
020$a | 020$q | If (RegFind(@020$a, '\\(') >= 0) Then S;
                RegReplace('(.*)\\(', ''); RegReplace('\\)', '');
                Table ('Sidosasut.tbl')
020$c | <DEL  | S
020$? | 020$? | Replace('.,' By ',');
                Delete('(');
                Delete(')');
                Delete('.', At Ending);
                Table ('Sidosasut.tbl')

#if (RegFind(@040$e, 'rda') >= 0) \
     And (Exists(@040$a) Or Exists(@040$b) Or Exists(@040$c) Or Exists(@040$d))
040I1 | 040I1   | S
040I2 | 040I2   | S
040$e | <DEL    | S
040$? | 040$?   | S
#endif

#if (RegFind(@040$e, 'rda') < 0)
040I1 | 040I1   | S
040I2 | 040I2   | S
040$? | 040$?   | S
#endif

222I1 | 222I1   | S
222I2 | 222I2   | S
222   | 222     | S; RegReplace('verkkoaineisto', 'verkkojulkaisu')

250I1 | 250I1   | S
250I2 | 250I2   | S
250   | 250     | LYHENTEET; Replace('..' By '.')

264I1    | 260I1    | ' '
264I2    | 260I2    | ' '
264(1)$a | 260$a    | S
264(1)$b | 260$b    | KUSTANNUSOY 

#if (@264I2 = '1')
264(n)$c | 260(1)$c    | S; Delete ('['); Delete (']'); Delete ('.')
#endif

#if (@264I2 = '3')
264I1    | 260(new)I1     | S
264I2    | 260(newest)I2  | '1'
264(n)$a | 260(1)$e    | If (Not Exists(@264(2)$b)) Then '(' + S + ')' Else '(' + S
#endif

264(2)$b | 260(no)$f    | KUSTANNUSOY; Delete (','); S + ')'

264$c | 260$c     | If (Not Exists(%260)) Then S; Delete ('['); Delete (']'); Delete ('.')

#if (Exists(@264(2)$c))
264(1)$c | <X01$c  | If (RegFind(S, '\\d+') >=0 ) Then RegMatch(0)
X01$c    | 260$c   | If (RegFind(@264(2)$c, '\\d+') >= 0) And (RegMatch(0) = S) Then S
#endif

300I1 | 300I1   | S
300I2 | 300I2   | S
300   | 300     | LYHENTEET;
                  If (RegFind(S, 'numeroimatonta') >= 0) Then 
                  {
                      RegReplace('\\s+', ' ');
                      If (RegFind(S, '(\\d+)\\s*numeroimatonta') >= 0) Then RegReplace('\\d+\\s*numeroimatonta', '[' + RegMatch(1) + ']');
                  }
                  Else S
336I1 | 336I1   | S
336I2 | 336I2   | S
336$a | <A36(1)$a(nso)   | Table ('Sisaltotyypit_336.tbl') + '.'
A36   | 336$a   | S; Delete ('.', At Ending)
336$b | <DEL    | S
336$2 | <DEL    | S

337I1 | 337I1   | S
337I2 | 337I2   | S
337$a | 337$a   | Table ('Mediatyypit_337.tbl')
337$b | <DEL    | S
337$2 | <DEL    | S

338I1 | <DELI1  | S
338I2 | <DELI2  | S
338   | <DEL    | S

CATI1 | <DELI1  | S
CATI2 | <DELI2  | S
CAT   | <DEL    | S

LOWI1 | <DELI1  | S
LOWI2 | <DELI2  | S
LOW   | <DEL    | S

SIDI1 | <DELI1  | S
SIDI2 | <DELI2  | S
SID   | <DEL    | S

#if (TAG != 'DEL')
???I1           | ???I1           | S
???I2           | ???I2           | S
???             | ???             | S
#endif
