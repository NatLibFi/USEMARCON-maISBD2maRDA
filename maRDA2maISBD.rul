MARC 21 (RDA) --> MARC 21 (ISBD)
Kansalliskirjasto / 19.4.2016 (TV)

#define macro LYHENTEET | Replace('painos' By 'p.'); \
                          Replace('karttoja' By 'kartt.'); \
                          Replace('nuotteja' By 'nuott.'); \
                          Replace('kuvitettu' By 'kuv.'); \
                          Replace('sivua' By 's.'); \
                          Replace('painos' By 'p.')

#define macro TUNNUKSET | RegReplace(' Oy', ''); \
                          RegReplace(' A[Bb]', ''); \
                          RegReplace(' Ltd', '')

#define macro KUSTANNUSOY   | Replace(' Oy' By ''); \
                              Replace(' Ab' By ''); \
                              Replace(' AB' By ''); \
                              Replace(' Ltd' By ''); \
                              Replace(' Kustannusosakeyhtiö' By ''); \
                              Replace(' Kustannus' By '')

000/1-18/ | 000/1-18/ | S
000/19/   | 000/19/ | 'i'
000/20-24/  | 000/20-24/ | S

008/1-6/   | 008/1-6/       | S
008/7/     | 008/7/         | Table ('aikaindy.tbl')
008/8-11/  | 008/8-11/      | S

#if (@008/7/ = 't')
008/12-15/  | 008/12-15/    | '    '
#endif

#if (Not @008/7/ = 't')
008/12-15/  | 008/12-15/    | S
#endif

008/16-18/ | 008/16-18/     | Table ('maakoodity.tbl')
008/36-38/ | 008/36-38/     | Table ('kielikoodit.tbl')
008/39-40/ | 008/39-40/     | '  '

015I1 | 015I1 | S
015I2 | 015I2 | S
015$q | 015$q | Replace('.,' By ',');
                Delete('(');
                Delete(')');
                Delete('.', At Ending);
                Table ('Sidosasut.tbl')
015$? | 015$? | S

020I1 | 020I1 | S
020I2 | 020I2 | S
020$a | 020$a | S; RegReplace('[\\s](.*)', '')
020$a | 020$q | If (RegFind(@020$a, '\\(') >= 0) Then S;
                RegReplace('(.*)\\(', ''); RegReplace('\\)', '');
                Table ('Sidosasut.tbl')
020$c | <DEL  | S
020$? | 020$? | Replace('.,' By ',');
                Delete('(');
                Delete(')');
                Delete('.', At Ending);
                Table ('Sidosasut.tbl')

#if (RegFind(@040$e, 'rda') >= 0) \
     And (Exists(@040$a) Or Exists(@040$b) Or Exists(@040$c) Or Exists(@040$d))
040I1 | 040I1   | S
040I2 | 040I2   | S
040$e | <DEL    | S
040$? | 040$?   | S
#endif

#if (RegFind(@040$e, 'rda') < 0)
040I1 | 040I1   | S
040I2 | 040I2   | S
040$? | 040$?   | S
#endif

222I1 | 222I1   | S
222I2 | 222I2   | S
222   | 222     | S; RegReplace('verkkoaineisto', 'verkkojulkaisu')

250I1 | 250I1   | S
250I2 | 250I2   | S
250   | 250     | LYHENTEET

// VUOSILUVUN VALINTA:
// 
//   -jos 264  1 $c:ssä on vuosiluku ilman hakasia otetaan se -> 260 $c
//   - samoin jos on vain pelkkä 264 1 $c eikä muita 264 $c osakenttiä
//   -jos 264 1 $c:ssä on vuosiluku hakasissa ja 264 3 $c:ssä vuosi ilman hakasia, 264 3 $c vuosi -> 260 $c
//   -jos 264 1 $c:ssä on vuosiluku hakasissa, eikä ole 264 3 $c, ja jos 264 4 $c:ssä oleva vuosi on sama kuin 264 1 $c:ssä, copyrightvuosi 264 4 $c:stä -> 260 $c

#if (Not (Exists (@245(1)$n) Or Exists (@245(1)$p))) And (@264(n)I2 = '1') And (Not Exists(@264(2)$c))
264(n)$c | 260(1)$c    | If ( (RegFind(S, '\\]') < 0) Or (Not Exists(@264(2)$c)) ) Then S; \ 
                         Delete ('['); Delete (']'); Delete ('.')
#endif

264I1 | 260I1    | ' '
264I2 | 260I2    | ' '
264(1)$a | 260$a    | S
264(1)$b | 260$b    | KUSTANNUSOY 

#if (@264(n)I2 = '3')
//264I1    | 260(new)I1     | S
//264I2    | 260(newest)I2  | '1'
264(n)$a | 260(no)$e    | If (Not Exists(@264(n)$b)) Then \
                            '(' + S + ')' \
                          Else '(' + S; Delete (',')
#endif

264(2)$b | 260(no)$f    | KUSTANNUSOY; Delete (','); S + ')'

264$c | 260$c     | If (Not Exists(%260)) Then S; Delete ('['); Delete (']'); Delete ('.')

#if (@264I2 = '4')
264(n)$c | 260(1)$c    | If (RegFind(@264(1)$c, '\\[') >=0) And (Not Exists(@264(2)$c)) And (RegFind(S, '\\d{4}') >= 0) Then '© ' + RegMatch(0)
#endif

// Jos 264$c-kentän toistumiin on kirjattu sama vuosi julkaisu- ja tekijänoikeusvuodeksi --> 264$c '© vuosi'

264   | 260 | Sort('abcef')

// Jos 264$c-kentän toistumiin on kirjattu sama vuosi julkaisu- ja tekijänoikeusvuodeksi --> 264$c '© vuosi'

#if (Exists(@264(2)$c))
264(1)$c | <X01$c  | If (RegFind(S, '\\d+') >=0 ) Then RegMatch(0)
X01$c    | 260$c   | If (RegFind(@264(2)$c, '\\d+') >= 0) And (RegMatch(0) = S) Then S
#endif

264      | 260   | RegReplace('[Kk]ustannuspaikka tuntematon', 'S.l.'); \
                   RegReplace('[Kk]ustantaja tuntematon', 's.n.'); \
                   Sort('abcdef')

300I1 | 300I1   | S
300I2 | 300I2   | S
300   | 300     | LYHENTEET

336I1 | 336I1   | S
336I2 | 336I2   | S
336$a | 336$a   | Table ('Sisaltotyypit_336.tbl')
336$b | <DEL    | S
336$2 | <DEL    | S

337I1 | 337I1   | S
337I2 | 337I2   | S
337$a | 337$a   | Table ('Mediatyypit_337.tbl')
337$b | <DEL    | S
337$2 | <DEL    | S

338I1 | <DELI1  | S
338I2 | <DELI2  | S
338   | <DEL    | S

530I1 | 530I1 | S
530I2 | 530I2 | S
530   | 530   | S; RegReplace('Verkkoaineisto', 'Verkkojulkaisu'); \
                   RegReplace('verkkoaineistona', verkkojulkaisuna')

76?I1 | 76?I1 | S
76?I2 | 76?I2 | S
76?$c | 76?$c | S; RegReplace('verkkoaineisto', 'verkkojulkaisu')
76?   | 76?   | S

77?I1 | 77?I1 | S
77?I2 | 77?I2 | S
77?$c | 77?$c | S; RegReplace('verkkoaineisto', 'verkkojulkaisu')
77?   | 77?   | S

78?I1 | 78?I1 | S
78?I2 | 78?I2 | S
78?$c | 78?$c | S; RegReplace('verkkoaineisto', 'verkkojulkaisu')
78?   | 78?   | S

856I1 | 856I1 | S
856I2 | 856I2 | S
856   | 856   | S; RegReplace('verkkoaineistoon', 'verkkojulkaisuun')

#if (TAG != 'DEL')
???I1           | ???I1           | S
???I2           | ???I2           | S
???             | ???             | S
#endif
